import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'groovy'
    id 'maven-publish'
    id 'org.nosphere.gradle.github.actions' version '1.3.2'
    id 'com.github.rodm.teamcity-server' version '1.4' // see https://github.com/rodm/gradle-teamcity-plugin
    id 'com.github.rodm.teamcity-environments' version '1.4.1'
}

ext {
    CI = System.getenv().containsKey('CI') || project.properties.containsKey('CI')
    RELEASE = project.properties.containsKey('RELEASE')
    baseVersion = '0.22'
    buildTime = new Date()
}

def buildTimestamp = CI ? timestamp(buildTime) : 'prerelease'
def snapshotVersion = RELEASE ? '' : buildTimestamp

group = 'nu.studer'
version = baseVersion + (snapshotVersion ? "-$snapshotVersion" : '')

dependencies {
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.google.guava:guava:31.0.1-jre'

    agent project(path: ':agent', configuration: 'plugin')

    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

test {
    useJUnitPlatform()
}

teamcity {
    version = '2020.2' // TeamCity API version

    server {
        descriptor {
            name = 'GradleBuildScanIntegration'
            displayName = 'Integration for Gradle and Maven build scans'
            description = 'Provides easy navigation from TeamCity builds to Gradle and Maven build scans'
            version = project.version
            vendorName = 'Etienne Studer'
            vendorUrl = 'https://github.com/etiennestuder'

            // optional properties
            email = 'etienne@studer.nu'
            downloadUrl = 'https://github.com/etiennestuder/teamcity-build-scan-plugin'
            useSeparateClassloader = true
        }

        publish {
            channels = ['Stable']
            token = findProperty('teamCityPluginPublishToken')
        }
    }

    environments {
        downloadsDir = mkdir('.teamcity/dists')
        baseHomeDir = mkdir('.teamcity/servers')
        baseDataDir = mkdir('.teamcity/data')

        teamcity2020_2 {
            version = '2020.2.2'
            serverOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5005'
            agentOptions '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=5006'
        }
    }
}

task sources(type: Jar) {
    from sourceSets.main.allJava
}

publishing.publications {
    PluginPublication(MavenPublication) {
        artifact serverPlugin
        artifact sources {
            classifier 'sources'
        }
    }
}

static timestamp(Date date) {
    def timestampFormat = new SimpleDateFormat('yyyyMMddHHmmss')
    timestampFormat.timeZone = TimeZone.getTimeZone('UTC')
    timestampFormat.format(date)
}
