import org.gradle.util.GradleVersion

// finish early if the build is not run with the minimum supported Gradle version
if (GradleVersion.current() < GradleVersion.version('4.1')) {
    logger.warn("TeamCity Build Scan plugin requires at least Gradle 4.1. Build uses Gradle ${GradleVersion.current()}.")
    return
}

// finish early if the build is an included build of a composite build
def isTopLevelBuild = !gradle.parent
if (!isTopLevelBuild) {
    return
}

// conditionally apply the GE / Build Scan plugin to the classpath so it can be applied to the build further down in this script
initscript {
    def atLeastGradle5 = GradleVersion.current() >= GradleVersion.version('5.0')
    def gePluginVersion = System.getProperty('teamCityBuildScanPlugin.gradle-enterprise.plugin.version')
    def ccudPluginVersion = System.getProperty('teamCityBuildScanPlugin.ccud.plugin.version')

    if (gePluginVersion || ccudPluginVersion && atLeastGradle5) {
        repositories {
            maven { url 'https://plugins.gradle.org/m2' }
        }
    }

    dependencies {
        if (gePluginVersion) {
            classpath atLeastGradle5 ?
                    "com.gradle:gradle-enterprise-gradle-plugin:$gePluginVersion" :
                    "com.gradle:build-scan-plugin:1.16"
        }

        if (ccudPluginVersion && atLeastGradle5) {
            classpath "com.gradle:common-custom-user-data-gradle-plugin:$ccudPluginVersion"
        }
    }
}

def BUILD_SCAN_PLUGIN_ID = 'com.gradle.build-scan'
def BUILD_SCAN_PLUGIN_CLASS = 'com.gradle.scan.plugin.BuildScanPlugin'

def GRADLE_ENTERPRISE_PLUGIN_ID = 'com.gradle.enterprise'
def GRADLE_ENTERPRISE_PLUGIN_CLASS = 'com.gradle.enterprise.gradleplugin.GradleEnterprisePlugin'
def GRADLE_ENTERPRISE_EXTENSION_CLASS = 'com.gradle.enterprise.gradleplugin.GradleEnterpriseExtension'

def CCUD_PLUGIN_ID = 'com.gradle.common-custom-user-data-gradle-plugin'
def CCUD_PLUGIN_CLASS = 'com.gradle.CommonCustomUserDataGradlePlugin'

def atLeastGradle5 = GradleVersion.current() >= GradleVersion.version('5.0')
def geUrl = System.getProperty('teamCityBuildScanPlugin.gradle-enterprise.url')
def gePluginVersion = System.getProperty('teamCityBuildScanPlugin.gradle-enterprise.plugin.version')
def ccudPluginVersion = System.getProperty('teamCityBuildScanPlugin.ccud.plugin.version')

// send a message to the server that the build has started
logger.quiet(generateBuildScanLifeCycleMessage('BUILD_STARTED'))

// define a buildScanPublished listener that captures the build scan URL and sends it in a message to the server
def buildScanPublishedAction = { def buildScan ->
    if (buildScan.metaClass.respondsTo(buildScan, 'buildScanPublished', Action)) {
        buildScan.buildScanPublished { scan ->
            logger.quiet(generateBuildScanLifeCycleMessage("BUILD_SCAN_URL:${scan.buildScanUri.toString()}"))
        }
    }
}

// register buildScanPublished listener and optionally apply the GE / Build Scan plugin
if (GradleVersion.current() < GradleVersion.version('6.0')) {
    rootProject {
        buildscript.configurations.getByName("classpath").incoming.afterResolve { ResolvableDependencies incoming ->
            def resolutionResult = incoming.resolutionResult
            def appliedBuildScanPlugin = false

            if (gePluginVersion) {
                def scanPluginComponent = resolutionResult.allComponents.find {
                    it.moduleVersion.with { group == "com.gradle" && (name == "build-scan-plugin" || name == "gradle-enterprise-gradle-plugin") }
                }
                if (!scanPluginComponent) {
                    pluginManager.apply(initscript.classLoader.loadClass(BUILD_SCAN_PLUGIN_CLASS))
                    appliedBuildScanPlugin = true
                    buildScan.publishAlways()
                }
            }

            if (ccudPluginVersion && atLeastGradle5) {
                def ccudPluginComponent = resolutionResult.allComponents.find {
                    it.moduleVersion.with { group == "com.gradle" && name == "common-custom-user-data-gradle-plugin" }
                }

                if (!ccudPluginComponent) {
                    def isAppliableCcudVersion = isVersionGreaterThanOrEqualTo(ccudPluginVersion, '1.6.6')
                    if (isAppliableCcudVersion || appliedBuildScanPlugin) {
                        pluginManager.apply(initscript.classLoader.loadClass(CCUD_PLUGIN_CLASS))
                    } else {
                        logger.warn("Cannot apply $CCUD_PLUGIN_ID versions less than 1.6.6 in init scripts when project applied Build Scan / Gradle Enterprise plugin (attempted version $ccudPluginVersion)")
                    }
                }
            }
        }

        afterEvaluate {
            pluginManager.withPlugin(BUILD_SCAN_PLUGIN_ID) {
                if (geUrl) {
                    buildScan.server = geUrl
                }

                buildScanPublishedAction(buildScan)
            }
        }
    }
} else {
    gradle.settingsEvaluated { settings ->
        def appliedGradleEnterprisePlugin = false

        if (gePluginVersion) {
            if (!settings.pluginManager.hasPlugin(GRADLE_ENTERPRISE_PLUGIN_ID)) {
                settings.pluginManager.apply(initscript.classLoader.loadClass(GRADLE_ENTERPRISE_PLUGIN_CLASS))
                appliedGradleEnterprisePlugin = true
                extensionsWithPublicType(settings, GRADLE_ENTERPRISE_EXTENSION_CLASS).collect { settings[it.name] }.each { ext ->
                    ext.buildScan.publishAlways()
                }
            }
        }

        if (ccudPluginVersion && atLeastGradle5) {
            if (!settings.pluginManager.hasPlugin(CCUD_PLUGIN_ID)) {
                def isAppliableCcudVersion = isVersionGreaterThanOrEqualTo(ccudPluginVersion, '1.6.6')
                if (isAppliableCcudVersion || appliedGradleEnterprisePlugin) {
                    settings.pluginManager.apply(initscript.classLoader.loadClass(CCUD_PLUGIN_CLASS))
                } else {
                    logger.warn("Cannot apply $CCUD_PLUGIN_ID versions less than 1.6.6 in init scripts when project applied Build Scan / Gradle Enterprise plugin (attempted version $ccudPluginVersion)")
                }
            }
        }

        extensionsWithPublicType(settings, GRADLE_ENTERPRISE_EXTENSION_CLASS).collect { settings[it.name] }.each { ext ->
            if (geUrl) {
                ext.server = geUrl
            }

            buildScanPublishedAction(ext.buildScan)
        }
    }
}

static def extensionsWithPublicType(def container, String publicType) {
    container.extensions.extensionsSchema.elements.findAll { it.publicType.concreteClass.name == publicType }
}

static String generateBuildScanLifeCycleMessage(def attribute) {
    return "##teamcity[nu.studer.teamcity.buildscan.buildScanLifeCycle '${escape(attribute as String)}']" as String
}

static String escape(String value) {
    return value?.toCharArray()?.collect { ch -> escapeChar(ch) }?.join()
}

static String escapeChar(char ch) {
    String escapeCharacter = "|"
    switch (ch) {
        case '\n': return escapeCharacter + "n"
        case '\r': return escapeCharacter + "r"
        case '|': return escapeCharacter + "|"
        case '\'': return escapeCharacter + "\'"
        case '[': return escapeCharacter + "["
        case ']': return escapeCharacter + "]"
        default: return ch < 128 ? ch as String : escapeCharacter + String.format("0x%04x", (int) ch)
    }
}

static boolean isVersionGreaterThanOrEqualTo(String version, String referenceVersion) {
    def versionNumbers = version.tokenize('.')*.toInteger()
    def referenceNumbers = referenceVersion.tokenize('.')*.toInteger()

    for (i in 0..Math.min(versionNumbers.size(), referenceNumbers.size())) {
        if (versionNumbers[i] != referenceNumbers[i]) {
            return versionNumbers[i] > referenceNumbers[i]
        }
    }

    return true
}
