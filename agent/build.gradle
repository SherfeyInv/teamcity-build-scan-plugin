plugins {
    id 'groovy'
    id 'com.github.rodm.teamcity-agent'
}

repositories {
    mavenCentral()
}

configurations {
    mvnExtension {
        canBeConsumed = false
        canBeResolved = true
    }
    teamCityInitClasspath {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    mvnExtension project(path: ':agent:service-message-maven-extension', configuration: 'mvnExtension')
    teamCityInitClasspath 'org.jetbrains.teamcity:serviceMessages:2020.2.1'

    testImplementation gradleTestKit()
    testImplementation ('io.ratpack:ratpack-groovy-test:1.9.0') {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-smile:2.12.1'
    testImplementation('org.spockframework:spock-core:2.0-groovy-3.0')
}

processResources {
    from configurations.mvnExtension
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
        String javaCompilerVersion = findProperty('javaCompilerVersion') ?: '8'
        languageVersion = JavaLanguageVersion.of(javaCompilerVersion)
    }
}

test {
    jvmArgumentProviders.add(new TeamCityInitClasspathCommandLineArgumentProvider(teamCityInitClasspath: configurations.teamCityInitClasspath))
}

teamcity {
    agent {
        archiveName = 'teamcity-build-scan-plugin-agent.zip'
        descriptor {
            pluginDeployment {
                useSeparateClassloader = true
            }
        }
    }
}

final class TeamCityInitClasspathCommandLineArgumentProvider implements CommandLineArgumentProvider {

    @Classpath
    FileCollection teamCityInitClasspath

    @Override
    Iterable<String> asArguments() {
        ["-DteamCityInitClasspath=$teamCityInitClasspath.asPath"]
    }

}
